# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/idl_tool/idl_config.gni")

idl_interface_sources = [
  "${target_gen_dir}/test_server_interface_proxy.cpp",
  "${target_gen_dir}/test_server_interface_stub.cpp",
]

idl_gen_interface("test_server_interface") {
  src_idl = rebase_path("ITestServerInterface.idl")
  dst_file = string_join(",", idl_interface_sources)
  log_domainid = "0xD003110"
  log_tag = "TestServerIPC"
}

config("test_server_service_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "service",
    "client",
    "utils",
    "${target_gen_dir}",
  ]
  cflags_cc = [ "-fexceptions" ]
}

ohos_shared_library("test_server_service") {
  configs = [ ":test_server_service_config" ]

  output_values = get_target_outputs(":test_server_interface")
  sources = [
    "${target_gen_dir}/test_server_interface_stub.cpp",
    "service/test_server_service.cpp",
  ]
  sources += filter_include(output_values, [ "test_server_interface_stub.cpp" ])

  deps = [ ":test_server_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "pasteboard:pasteboard_client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  part_name = "arkxtest"
  subsystem_name = "testfwk"
}

config("test_server_client_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "client",
    "utils",
    "${target_gen_dir}",
  ]
}

ohos_shared_library("test_server_client") {
  public_configs = [ ":test_server_client_config" ]

  output_values = get_target_outputs(":test_server_interface")
  sources = [
    "${target_gen_dir}/test_server_interface_proxy.cpp",
    "client/test_server_client.cpp",
  ]
  sources +=
      filter_include(output_values, [ "test_server_interface_proxy.cpp" ])

  deps = [ ":test_server_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  part_name = "arkxtest"
  subsystem_name = "testfwk"
}
